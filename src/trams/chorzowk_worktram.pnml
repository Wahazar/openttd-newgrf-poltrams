// part of Polish Tram Set
// Graphic by McZapkie
// NML code by McZapkie

spriteset(spriteset_K_worktram_purchase, "gfx/ChorzowK-worktram.png") { template_sprite_purchase_consist(28,104) }
spriteset(spriteset_K_worktram_depot, "gfx/ChorzowK-worktram.png") { template_sprite_window_sideview8(6) }
spriteset(spriteset_K_worktram, "gfx/ChorzowK-worktram.png") { template_sprite8(6) }

switch(FEAT_ROADVEHS, SELF, K_worktram_graphic_switch, extra_callback_info1) {
	0x00:		spriteset_K_worktram;
	0x10..0x12:	spriteset_K_worktram_depot;
	return spriteset_K_worktram_purchase;
}

#define CHORZOW_CARGO_TRAM_LOAD 12

switch(FEAT_ROADVEHS, SELF, maxload_chorzow_cargo_tram_switch, cargo_type_in_veh) {    
	GOOD: return CHORZOW_CARGO_TRAM_LOAD*8/4;  
	ENSP: return CHORZOW_CARGO_TRAM_LOAD*8/5;  
	FMSP: return CHORZOW_CARGO_TRAM_LOAD*8/4;  
	MNSP: return CHORZOW_CARGO_TRAM_LOAD*8/3;    
        return CHORZOW_CARGO_TRAM_LOAD;  
} 

if (param_cargo_worktrams == 1) {
item(FEAT_ROADVEHS, ChorzowK_worktram) {
    property {
        /* common properties */
        name:                           string(STR_CHORZOWK_WORKTRAM_NAME);
        climates_available:             POLTRAM_CLIMATES;
        introduction_date:              date(1952, 1, 1);            
        model_life:                     get_model_life(1952,1985,5);
        vehicle_life:                   20;
	retire_early:			5;
        reliability_decay:              20;
        loading_speed:                  4;
        cost_factor:                    CHORZOWK_COST /2;      
        running_cost_factor:            tram_running_cost(CHORZOWK_MASS, CHORZOWK_VMAX/2, RHEOSTAT, HARD_COMFORT);
        /* tram properties */
        sprite_id:                      SPRITE_ID_NEW_ROADVEH;
        speed:                          CHORZOWK_VMAX km/h;
        refittable_cargo_classes:       bitmask(CC_EXPRESS, CC_MAIL, CC_ARMOURED);
	non_refittable_cargo_classes:   bitmask(CC_LIQUID, CC_PASSENGERS, CC_BULK, CC_OVERSIZED);
        misc_flags:                     bitmask(ROADVEH_FLAG_TRAM,ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_2CC);
        refit_cost:                     0;
        cargo_age_period:               185;
        power:                          80 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        cargo_capacity:                 CHORZOW_CARGO_TRAM_LOAD;
        weight:                         CHORZOWK_MASS ton;
        tractive_effort_coefficient:    0.3;
        air_drag_coefficient:           0.2;
        visual_effect:      		visual_effect(VISUAL_EFFECT_ELECTRIC, 0);
        length:      			8;
    }

    graphics {
	default:            	K_worktram_graphic_switch;
	purchase:           	spriteset_K_worktram_purchase;
	cargo_capacity:		maxload_chorzow_cargo_tram_switch;
	additional_text: 	return string(STR_CARGO_TRAM);
    }
}
} //if
