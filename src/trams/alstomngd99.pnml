// part of Polish Tram Set
// Graphic by Sojita
// NML code by McZapkie

spriteset(spriteset_NGd99_purchase, "gfx/AlstomNg99.png") { template_sprite_purchase_consist(73,58) }

spriteset(spriteset_NGd99a, "gfx/AlstomNg99.png") { template_sprite7(6) }
spriteset(spriteset_NGd99b, "gfx/AlstomNg99.png") { template_sprite2(29) }
spriteset(spriteset_NGd99c, "gfx/AlstomNg99.png") { template_sprite7(51) }

spriteset(spriteset_depot_NGd99a, "gfx/AlstomNg99.png") { template_sprite_window_sideview7(6) }
spriteset(spriteset_depot_NGd99b, "gfx/AlstomNg99.png") { template_sprite_window_sideview2(29) }
spriteset(spriteset_depot_NGd99c, "gfx/AlstomNg99.png") { template_sprite_window_sideview7(51) }

switch (FEAT_ROADVEHS, SELF, AlstomNGd99_articulated_switch, extra_callback_info1) {
    1..2: return AlstomNGd99;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//display on tracks
switch(FEAT_ROADVEHS, SELF, AlstomNGd99_consist_graphic_switch, position_in_consist) {
	0:    spriteset_NGd99a;
	1:    spriteset_NGd99b;
	2:    spriteset_NGd99c;
	return CB_FAILED;
}

//display in depot window
switch(FEAT_ROADVEHS, SELF, AlstomNGd99_consist_depot_switch, position_in_consist) {
	0:    spriteset_depot_NGd99a;
	1:    spriteset_depot_NGd99b;
	2:    spriteset_depot_NGd99c;
	return CB_FAILED;
}
//track or window display
switch(FEAT_ROADVEHS, SELF, AlstomNGd99_graphic_switch, extra_callback_info1) {
	0x00:			AlstomNGd99_consist_graphic_switch;
	0x10..0x12:		AlstomNGd99_consist_depot_switch;
	return spriteset_NGd99_purchase;
}

switch(FEAT_ROADVEHS, SELF, switch_AlstomNGd99_length, position_in_consist) {
	0: return 6;
	1: return 2;
	2: return 6;
	return CB_FAILED;
}

switch(FEAT_ROADVEHS, SELF, switch_AlstomNGd99_visual_effect, position_in_consist) {
	0: return visual_effect(VISUAL_EFFECT_ELECTRIC, -2);
	1..2: return visual_effect(VISUAL_EFFECT_DISABLE, 0);
	return CB_FAILED;
}

#define ALSTOMNGD99_VMAX 70
#define ALSTOMNGD99_MASS 30

tram_running_cost_switch(AlstomNGd99, ALSTOMNGD99_MASS, CLIMA_COMFORT)

CARGO_DECAY_SWITCH(CLIMA_COMFORT, ALSTOMNGD99) 

switch(FEAT_ROADVEHS, SELF, capacity_AlstomNGd99_touristic_switch, cargo_type_in_veh) {  
	TOUR: return ALSTOMNGD99_SIT/3;  
	PASS: return ALSTOMNGD99_CAP/3;  
        return ALSTOMNGD99_SIT/3;  
}  

if (param_electric_passenger_trams == 1) {
item(FEAT_ROADVEHS, AlstomNGd99) {
    property {
        name:                           string(STR_ALSTOMNGD99_NAME);
        climates_available:             POLTRAM_CLIMATES;
        introduction_date:              date(1999, 9, 9);            
        model_life:                     get_model_life(1999,2016,18);
        vehicle_life:                   20;
        retire_early:                   18;
        reliability_decay:              20;
        loading_speed:                  get_loading_speed(9, 70) / 3; //total 4 double+one single door, 70% of lowered floor; avg per unit
        cost_factor:                    ALSTOMNGD99_COST;      
        running_cost_factor:            tram_running_cost(ALSTOMNGD99_MASS, ALSTOMNGD99_VMAX, ASYNCHRONOUS, CLIMA_COMFORT);      
        sprite_id:                      SPRITE_ID_NEW_ROADVEH;
        speed:                          ALSTOMNGD99_VMAX km/h;
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        misc_flags:                     bitmask(ROADVEH_FLAG_TRAM, ROADVEH_FLAG_AUTOREFIT, ROADVEH_FLAG_2CC);
        refit_cost:                     0;
        cargo_age_period:               CARGO_DECAY(CLIMA_COMFORT, ALSTOMNGD99_CAP,ALSTOMNGD99_SIT);
        power:                          560 kW; 
        running_cost_base:              RUNNING_COST_ELECTRIC;
        cargo_capacity:                 ALSTOMNGD99_CAP / 3; //per unit
        weight:                         ALSTOMNGD99_MASS ton; 
        tractive_effort_coefficient:    0.3;
        air_drag_coefficient:           0.15;
        visual_effect:      		    visual_effect(VISUAL_EFFECT_ELECTRIC, 0);
	//sound_effect:  sound("sfx/159156__heigh-hoo__tram-matsuyama8.wav");
        length:      			    6;
    }

    graphics {
	articulated_part:	AlstomNGd99_articulated_switch;
	default:            	AlstomNGd99_graphic_switch;
	purchase:           	spriteset_NGd99_purchase;
	length:			switch_AlstomNGd99_length;
	cargo_capacity:		capacity_AlstomNGd99_touristic_switch;
	purchase_cargo_capacity:  return ALSTOMNGD99_CAP/3;
	visual_effect: 		switch_AlstomNGd99_visual_effect;
	running_cost_factor:    switch_AlstomNGd99_dynamic_running_cost;
	purchase_running_cost_factor: return tram_running_cost(ALSTOMNGD99_MASS, ALSTOMNGD99_VMAX, ASYNCHRONOUS, SOFT_COMFORT); 
	cargo_age_period:	switch_ALSTOMNGD99_comfort;
	additional_text: 	return string(STR_DYNAMIC_RUNNING_COST, ALSTOMNGD99_SIT, ALSTOMNGD99_CAP-ALSTOMNGD99_SIT, get_total_loading_speed(9, 70, ALSTOMNGD99_CAP));
    }
}
} //if
